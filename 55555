# Setup VPC
resource "aws_vpc" "dev_proj_1_vpc_us_east_1" {
  cidr_block = var.vpc_cidr
  tags = {
    Name = var.vpc_name
  }
}

# Setup public subnet
resource "aws_subnet" "dev_proj_1_public_subnets" {
  count             = length(var.cidr_public_subnet)
  vpc_id            = aws_vpc.dev_proj_1_vpc_us_east_1.id
  cidr_block        = element(var.cidr_public_subnet, count.index)
  availability_zone = element(var.us_availability_zone, count.index)

  tags = {
    Name = "dev-proj-public-subnet-${count.index + 1}"
  }
}

# Setup private subnet
resource "aws_subnet" "dev_proj_1_private_subnets" {
  count             = length(var.cidr_private_subnet)
  vpc_id            = aws_vpc.dev_proj_1_vpc_us_east_1.id
  cidr_block        = element(var.cidr_private_subnet, count.index)
  availability_zone = element(var.us_availability_zone, count.index)

  tags = {
    Name = "dev-proj-private-subnet-${count.index + 1}"
  }
}

# Setup Internet Gateway
resource "aws_internet_gateway" "dev_proj_1_public_internet_gateway" {
  vpc_id = aws_vpc.dev_proj_1_vpc_us_east_1.id
  tags = {
    Name = "dev-proj-1-igw"
  }
}

# Public Route Table
resource "aws_route_table" "dev_proj_1_public_route_table" {
  vpc_id = aws_vpc.dev_proj_1_vpc_us_east_1.id
  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.dev_proj_1_public_internet_gateway.id
  }
  tags = {
    Name = "dev-proj-1-public-rt"
  }
}

# Public Route Table and Public Subnet Association
resource "aws_route_table_association" "dev_proj_1_public_rt_subnet_association" {
  count          = length(aws_subnet.dev_proj_1_public_subnets)
  subnet_id      = aws_subnet.dev_proj_1_public_subnets[count.index].id
  route_table_id = aws_route_table.dev_proj_1_public_route_table.id
}

# Setup Elastic IP for NAT Gateway
resource "aws_eip" "dev_proj_1_nat_eip" {
  vpc = true

  tags = {
    Name = "dev-proj-1-nat-eip"
  }
}

# Setup NAT Gateway
resource "aws_nat_gateway" "dev_proj_1_nat_gateway" {
  allocation_id = aws_eip.dev_proj_1_nat_eip.id
  subnet_id     = aws_subnet.dev_proj_1_public_subnets[0].id

  tags = {
    Name = "dev-proj-1-nat-gateway"
  }
}

# Private Route Table
resource "aws_route_table" "dev_proj_1_private_route_table" {
  vpc_id = aws_vpc.dev_proj_1_vpc_us_east_1.id
  route {
    cidr_block     = "0.0.0.0/0"
    nat_gateway_id = aws_nat_gateway.dev_proj_1_nat_gateway.id
  }
  tags = {
    Name = "dev-proj-1-private-rt"
  }
}

# Private Route Table and Private Subnet Association
resource "aws_route_table_association" "dev_proj_1_private_rt_subnet_association" {
  count          = length(aws_subnet.dev_proj_1_private_subnets)
  subnet_id      = aws_subnet.dev_proj_1_private_subnets[count.index].id
  route_table_id = aws_route_table.dev_proj_1_private_route_table.id
}

# Setup Network Interface
resource "aws_network_interface" "dev_proj_1_network_interface" {
  count       = length(var.cidr_public_subnet)
  subnet_id   = aws_subnet.dev_proj_1_public_subnets[count.index].id
  description = "Network interface for dev-proj-${count.index + 1}"

  tags = {
    Name = "dev-proj-${count.index + 1}-ni"
  }
}

# Attach Network Interface to an instance (example)
resource "aws_instance" "dev_proj_1_instance" {
  count = length(var.cidr_public_subnet)
  ami           = var.instance_ami
  instance_type = var.instance_type
  subnet_id     = aws_subnet.dev_proj_1_public_subnets[count.index].id

  network_interface {
    network_interface_id = aws_network_interface.dev_proj_1_network_interface[count.index].id
    device_index         = 1
  }

  tags = {
    Name = "dev-proj-instance-${count.index + 1}"
  }
}

# Create multiple target groups
resource "aws_lb_target_group" "dev_proj_1_lb_target_group" {
  for_each = { for idx, tg in var.lb_target_groups : idx => tg }
  name     = each.value.name
  port     = each.value.port
  protocol = each.value.protocol
  vpc_id   = each.value.vpc_id

  health_check {
    path                 = each.value.path
    port                 = each.value.port
    healthy_threshold    = 6
    unhealthy_threshold  = 2
    timeout              = 2
    interval             = 5
    matcher              = "200"  # has to be HTTP 200 or fails
  }
}

# Attach multiple EC2 instances to each target group
resource "aws_lb_target_group_attachment" "dev_proj_1_lb_target_group_attachment" {
  for_each = { for idx, instance_id in var.ec2_instance_ids : idx => instance_id }
  target_group_arn = aws_lb_target_group.dev_proj_1_lb_target_group[each.key].arn
  target_id        = each.value
  port             = 8080
}
.

variable "lb_target_groups" {
  description = "List of target groups configurations"
  type = list(object({
    name     = string
    port     = number
    protocol = string
    path     = string
    vpc_id   = string
  }))
}

variable "ec2_instance_ids" {
  description = "List of EC2 instance IDs to attach to the target groups"
  type = list(string)
}



